buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.12.1'
        classpath "com.netflix.nebula:gradle-extra-configurations-plugin:2.2.2"
        classpath "org.ysb33r.gradle:gradletest:0.5.4"
    }
}

apply plugin: 'groovy'
apply plugin: 'com.github.hierynomus.license'
apply plugin : 'org.ysb33r.gradletest'

group 'org.ysb33r.gradle'
version '0.9-SNAPSHOT'

sourceCompatibility = 1.7

repositories {
    jcenter()
}

ext {
    spockGroovyVer = GroovySystem.version.replaceAll(/\.\d+$/,'')
    mirahTestVer = '0.1.4'
    mirahTestRepo = "${buildDir}/test/mirahTestRepo"
}

configurations {
  mirahTesting
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    testCompile ("org.spockframework:spock-core:1.0-groovy-${spockGroovyVer}") {
        exclude module : 'groovy-all'
    }

    mirahTesting "org.mirah:mirah:${mirahTestVer}"
    gradleTest "org.mirah:mirah:${mirahTestVer}"
}

task prepareTestRepo( type : Copy ) {
    from configurations.mirahTesting
    into mirahTestRepo
}

test {
    dependsOn prepareTestRepo

    systemProperties TESTREPO : file(mirahTestRepo).absolutePath,
        TESTROOT : file("${buildDir}/test/mirah").absolutePath,
        TESTRESOURCES : file('src/test/resources/testcode').absolutePath

}

license {
    header = rootProject.file('config/apache-header')
    strictCheck = true
    ignoreFailures = false
    mapping {
        groovy ='SLASHSTAR_STYLE'
        mirah = 'SCRIPT_STYLE'
    }
    ext.year = '2015'
    excludes(['**/*.ad', '**/*.asciidoc', '**/*.adoc', '**/*.md','**/*.properties', '**/*.txt', '**/*.bz2'])
}

gradleTest {
    versions '2.0' ,'2.2', '2.3', '2.5', '2.8', '2.9'

    onlyIf { !gradle.startParameter.isOffline() }
}

plugins.withType(JavaPlugin) {
    project.tasks.withType(GroovyCompile) { task ->
        task.sourceCompatibility = project.sourceCompatibility
        task.targetCompatibility = project.targetCompatibility
    }
    project.tasks.withType(JavaCompile) { task ->
        task.sourceCompatibility = project.sourceCompatibility
        task.targetCompatibility = project.targetCompatibility
    }
}
